buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
	}
}

apply plugin: 'org.springframework.boot'

dependencies {
	implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.experimental:r2dbc-micrometer-spring-boot:1.0.1'
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// Default is OTel + Zipkin, you can opt in via "-Pwavefront" to Wavefront and "-Pbrave" for Brave
	if (project.hasProperty('wavefront')) {
		implementation 'io.micrometer:micrometer-tracing-reporter-wavefront'
	}
	if (project.hasProperty('brave')) {
		implementation 'io.micrometer:micrometer-tracing-bridge-brave'
		implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
	}
	else {
		implementation 'io.micrometer:micrometer-tracing-bridge-otel'
		implementation 'io.opentelemetry:opentelemetry-exporter-zipkin'
	}

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'io.r2dbc:r2dbc-h2'

	testImplementation project(':test-common')
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
